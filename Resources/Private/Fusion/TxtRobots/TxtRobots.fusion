prototype(Jonnitto.Sitemap:TxtRobots) < prototype(Neos.Fusion:Http.Message) {
    httpResponseHead.headers.Content-Type = 'text/plain;'
    body = Neos.Fusion:Template {
        templatePath = 'resource://Jonnitto.Sitemap/Private/Fusion/TxtRobots/TxtRobots.txt'

        sitemaps = Neos.Fusion:Case {
            hasLanguage {
                condition = ${Configuration.setting('Neos.ContentRepository.contentDimensions.language') != null}
                renderer = Neos.Neos:DimensionsMenu {
                    dimension = 'language'
                    templatePath = 'resource://Jonnitto.Sitemap/Private/Fusion/TxtRobots/Sitemap.html'
                    presets = ${Configuration.setting('Jonnitto.Sitemap.robotsTxt.dimensionsPresets')}
                    renderHiddenInIndex = true
                    includeAllPresets = true
                }
            }
            noLanguage {
                condition = true
                renderer = Neos.Fusion:Array {
                    sitemap = 'Sitemap: '
                    url = Neos.Neos:NodeUri {
                        absolute = true
                        format = 'xml.improvedsitemap'
                        node = ${site}
                    }
                }
            }
        }
    }
}
